
%%%%%%%%%%%%%%%%%%%%% chapter.tex %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% sample chapter
%
% Use this file as a template for your own input.
%
%%%%%%%%%%%%%%%%%%%%%%%% Springer-Verlag %%%%%%%%%%%%%%%%%%%%%%%%%%
%\motto{Use the template \emph{chapter.tex} to style the various elements of your chapter content.}

\chapter{Rosetta Code Tasks starting with Numbers}
\label{rosettacode-numbers}

\section*{100 doors}

Problem: You have 100 doors in a row that are all initially closed.
You make 100 passes by the doors. The first time through, you visit
every door and toggle the door (if the door is closed, you open it; if
it is open, you close it). The second time you only visit every 2nd
door (door \#2, \#4, \#6, \ldots{}). The third time, every 3rd door
(door \#3, \#6, \#9, \ldots{}), etc, until you only visit the 100th
door.

Question: What state are the doors in after the last pass? Which are
open, which are closed?

\textbf{Alternate:} As noted in this page's discussion page, the only
doors that remain open are whose numbers are perfect squares of
integers. Opening only those doors is an optimization that may also be
expressed.

\begin{wideverbatim}
unoptimized

(let Doors (need 100)
   (for I 100
      (for (D (nth Doors I)  D  (cdr (nth D I)))
         (set D (not (car D))) ) )
   (println Doors) )

optimized

(let Doors (need 100)
   (for I (sqrt 100)
      (set (nth Doors (* I I)) T) )
   (println Doors) )
\end{wideverbatim}

\pagebreak{}
\section*{24 game}

The 24 Game tests one's mental arithmetic.

Write a program that randomly chooses and displays four digits, each
from one to nine, with repetitions allowed. The program should prompt
for the player to enter an equation using \emph{just} those, and
\emph{all} of those four digits. The program should \emph{check} then
evaluate the expression. The goal is for the player to enter an
expression that evaluates to \textbf{24}.

\begin{itemize}
\item
  Only multiplication, division, addition, and subtraction
  operators/functions are allowed.
\item
  Division should use floating point or rational arithmetic, etc, to
  preserve remainders.
\item
  Brackets are allowed, if using an infix expression evaluator.
\item
  Forming multiple digit numbers from the supplied digits is
  \emph{disallowed}. (So an answer of 12+12 when given 1, 2, 2, and 1 is
  wrong).
\item
  The order of the digits when given does not have to be preserved.
\end{itemize}

Note:

\begin{itemize}
\item The type of expression evaluator used is not mandated. An RPN
  evaluator is equally acceptable for example.
\item
  The task is not for the program to generate the expression, or test
  whether an expression is even possible.
\end{itemize}

C.f: 24 game Player

\textbf{Reference}

\begin{enumerate}
\item
  \href{http://www.bbc.co.uk/dna/h2g2/A933121}{The 24 Game} on h2g2.
\end{enumerate}


\begin{wideverbatim}

(de checkExpression (Lst Exe)
   (make
      (when (diff Lst (fish num? Exe))
         (link "Not all numbers used" ) )
      (when (diff (fish num? Exe) Lst)
         (link "Using wrong number(s)") )
      (when (diff (fish sym? Exe) '(+ - * /))
         (link "Using illegal operator(s)") ) ) )

(loop
   (setq Numbers (make (do 4 (link (rand 1 9)))))
   (prinl
      "Please enter a Lisp expression using (, ), +, -, *, / and "
      (glue ", " Numbers) )
   (prin "Or a single dot '.' to stop: ")
   (T (= "." (setq Reply (catch '(NIL) (in NIL (read)))))
      (bye) )
   (cond
      ((str? Reply)
         (prinl "-- Input error: " Reply) )
      ((checkExpression Numbers Reply)
         (prinl "-- Illegal Expression")
         (for S @
            (space 3)
            (prinl S) ) )
      ((str? (setq Result (catch '(NIL) (eval Reply))))
         (prinl "-- Evaluation error: " @) )
      ((= 24 Result)
         (prinl "++ Congratulations! Correct result :-)") )
      (T (prinl "Sorry, this gives " Result)) )
   (prinl) )

\end{wideverbatim}

\begin{wideverbatim}

Output:

Please enter a Lisp expression using (, ), +, -, *, / and 1, 3, 3, 5
Or a single dot '.' to stop: (* (+ 3 1) (+ 5 1))
++ Congratulations! Correct result :-)

Please enter a Lisp expression using (, ), +, -, *, / and 8, 4, 7, 1
Or a single dot '.' to stop: (* 8 (\% 7 3) 9)
-- Illegal Expression
   Not all numbers used
   Using wrong number(s)
   Using illegal operator(s)

Please enter a Lisp expression using (, ), +, -, *, / and 4, 2, 2, 3
Or a single dot '.' to stop: (/ (+ 4 3) (- 2 2))
-- Evaluation error: Div/0

Please enter a Lisp expression using (, ), +, -, *, / and 8, 4, 5, 9
Or a single dot '.' to stop: .

\end{wideverbatim}

\pagebreak{}
\section*{24 game/Solve}


Write a function that given four digits subject to the rules of the
24 game, computes an expression to solve the game
if possible.

Show examples of solutions generated by the function

C.F: Arithmetic Evaluator


\begin{wideverbatim}

We use Pilog (PicoLisp Prolog) to solve this task

(be play24 (@Lst @Expr)                # Define Pilog rule
   (permute @Lst (@A @B @C @D))
   (member @Op1 (+ - * /))
   (member @Op2 (+ - * /))
   (member @Op3 (+ - * /))
   (or
      ((equal @Expr (@Op1 (@Op2 @A @B) (@Op3 @C @D))))
      ((equal @Expr (@Op1 @A (@Op2 @B (@Op3 @C @D))))) )
   (@ = 24 (catch '("Div/0") (eval (-> @Expr)))) )

(de play24 (A B C D)                   # Define PicoLisp function
   (pilog
      (quote
         @L (list A B C D)
         (play24 @L @X) )
      (println @X) ) )

(play24 5 6 7 8)                       # Call 'play24' function

Output:

(* (+ 5 7) (- 8 6))
(* 6 (+ 5 (- 7 8)))
(* 6 (- 5 (- 8 7)))
(* 6 (- 5 (/ 8 7)))
(* 6 (+ 7 (- 5 8)))
(* 6 (- 7 (- 8 5)))
(* 6 (/ 8 (- 7 5)))
(/ (* 6 8) (- 7 5))
(* (+ 7 5) (- 8 6))
(* (- 8 6) (+ 5 7))
(* (- 8 6) (+ 7 5))
(* 8 (/ 6 (- 7 5)))
(/ (* 8 6) (- 7 5))

\end{wideverbatim}

\pagebreak{}
\section*{99 Bottles of Beer}


In this puzzle, write code to print out the entire ``99 bottles of beer
on the wall'' song. For those who do not know the song, the lyrics
follow this form:

\begin{verbatim}
X bottles of beer on the wall
X bottles of beer
Take one down, pass it around
X-1 bottles of beer on the wall

X-1 bottles of beer on the wall
...
Take one down, pass it around
0 bottles of beer on the wall
\end{verbatim}

Where X and X-1 are replaced by numbers of course. Grammatical support
for ``1 bottle of beer'' is optional. As with any puzzle, try to do it
in as creative/concise/comical a way as possible (simple, obvious
solutions allowed, too).

See also:
\href{http://99-bottles-of-beer.net/}{http://99-bottles-of-beer.net/}


\begin{wideverbatim}

(de bottles (N)
   (case N
      (0 "No more beer")
      (1 "One bottle of beer")
      (T (cons N " bottles of beer")) ) )

(for (N 99 (gt0 N))
   (prinl (bottles N) " on the wall,")
   (prinl (bottles N) ".")
   (prinl "Take one down, pass it around,")
   (prinl (bottles (dec 'N)) " on the wall.")
   (prinl) )

\end{wideverbatim}




% \input{referenc}
