%%%%%%%%%%%%%%%%%%%%% chapter.tex %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% sample chapter
%
% Use this file as a template for your own input.
%
%%%%%%%%%%%%%%%%%%%%%%%% Springer-Verlag %%%%%%%%%%%%%%%%%%%%%%%%%%
%\motto{Use the template \emph{chapter.tex} to style the various elements of your chapter content.}



\chapter{Symbols starting with M}
\label{cha:functions-starting-with-M}

 
\section*{\texttt{*Msg}}
\label{sec:func-ref-M-*Msg}


A global variable holding the last recently issued error message. See
also \texttt{Error Handling}, \texttt{*Err} and \texttt{\textasciicircum{}}.


\begin{wideverbatim}
: (+ 'A 2)
!? (+ 'A 2)
A -- Number expected
?
:
: *Msg
-> "Number expected"
\end{wideverbatim}

 
\section*{\texttt{+Mis}}
\label{sec:func-ref-M-+Mis}


Prefix class to explicitly specify validation functions for
\texttt{+relation}s. Expects a function that takes a value and an entity object, and returns \texttt{NIL} if everything is correct, or an error string.
See also \texttt{Database}.


\begin{wideverbatim}
(class +Ord +Entity)            # Order class
(rel pos (+Mis +List +Joint)    # List of positions in that order
   ((Val Obj)
      (when (memq NIL Val)
         "There are empty positions" ) )
   ord (+Pos) )
\end{wideverbatim}

 
\section*{\texttt{(macro prg) -> any}}
\label{sec:func-ref-M-(macro prg) -> any}


Substitues all \texttt{pat?} symbols in \texttt{prg} (using \texttt{fill}), and executes the
result with \texttt{run}. Used occasionally to call functions which otherwise
do not evaluate their arguments.


\begin{wideverbatim}
: (de timerMessage (@N . @Prg)
   (setq @N (- @N))
   (macro
      (task @N 0 . @Prg) ) )
-> timerMessage
: (timerMessage 6000 (println 'Timer 6000))
-> (-6000 0 (println 'Timer 6000))
: (timerMessage 12000 (println 'Timer 12000))
-> (-12000 0 (println 'Timer 12000))
: (more *Run)
(-12000 2616 (println 'Timer 12000))
(-6000 2100 (println 'Timer 6000))
-> NIL
: Timer 6000
Timer 12000
...
\end{wideverbatim}

 
\section*{\texttt{(made ['lst1 ['lst2]]) -> lst}}
\label{sec:func-ref-M-(made ['lst1 ['lst2]]) -> lst}


Initializes a new list value for the current \texttt{make} environment. All
list elements already produced with \texttt{chain} and \texttt{link} are discarded,
and \texttt{lst1} is used instead. Optionally, \texttt{lst2} can be specified as the
new linkage cell, otherwise the last cell of \texttt{lst1} is used. When called
without arguments, \texttt{made} does not modify the environment. In any case,
the current list is returned.


\begin{wideverbatim}
: (make
   (link 'a 'b 'c)         # Link three items
   (println (made))        # Print current list (a b c)
   (made (1 2 3))          # Discard it, start new with (1 2 3)
   (link 4) )              # Link 4
(a b c)
-> (1 2 3 4)
\end{wideverbatim}

 
\section*{\texttt{(mail `any `cnt `sym1 `sym2|lst1 `sym3 `lst2 . prg)'}}
\label{sec:func-ref-M-(mail `any `cnt `sym1 `sym2|lst1 `sym3 `lst2 . prg)'}

Sends an eMail via SMTP to a mail server at host \texttt{any}, port \texttt{cnt}.
\texttt{sym1} should be the ``from'' address, \texttt{sym2|lst1} the ``to'' address(es),
and \texttt{sym3} the subject. \texttt{lst2} is a list of attachments, each one
specified by three elements for path, name and mime type. \texttt{prg}
generates the mail body with \texttt{prEval}. See also \texttt{connect}.


\begin{wideverbatim}
(mail "localhost" 25                               # Local mail server
   "a@bc.de"                                       # "From" address
   "abu@software-lab.de"                           # "To" address
   "Testmail"                                      # Subject
   (quote
      "img/go.png" "go.png" "image/png"            # First attachment
      "img/7fach.gif" "7fach.gif" "image/gif" )    # Second attachment
   "Hello,"                                        # First line
   NIL                                             # (empty line)
   (prinl (pack "This is mail #" (+ 3 4))) )       # Third line
\end{wideverbatim}

 
\section*{\texttt{(make .. [(made 'lst ..)] .. [(link 'any ..)] ..) -> any}}
\label{sec:func-ref-M-(make .. [(made 'lst ..)] .. [(link 'any ..)] ..) -> any}


Initializes and executes a list-building process with the \texttt{made},
\texttt{chain}, \texttt{link} and \texttt{yoke} functions, and returns the result list. For
efficiency, pointers to the head and the tail of the list are maintained
internally.


\begin{wideverbatim}
: (make (link 1) (link 2 3) (link 4))
-> (1 2 3 4)
: (make (made (1 2 3)) (link 4))
-> (1 2 3 4)
\end{wideverbatim}

 
\section*{\texttt{(map 'fun 'lst ..) -> lst}}
\label{sec:func-ref-M-(map 'fun 'lst ..) -> lst}


Applies \texttt{fun} to \texttt{lst} and all successive CDRs. When additional \texttt{lst}
arguments are given, they are passed to \texttt{fun} in the same way. Returns
the result of the last application. See also \texttt{mapc}, \texttt{maplist},
\texttt{mapcar}, \texttt{mapcon}, \texttt{mapcan} and \texttt{filter}.


\begin{wideverbatim}
: (map println (1 2 3 4) '(A B C))
(1 2 3 4) (A B C)
(2 3 4) (B C)
(3 4) (C)
(4) NIL
-> NIL
\end{wideverbatim}

 
\section*{\texttt{map/3}}
\label{sec:func-ref-M-map/3}


\emph{Pilog} predicate that returns a list and subsequent
CDRs of that list, after applying the \texttt{get} algorithm to that object and
the following arguments. Often used in database queries. See also
\texttt{lst/3}.


\begin{wideverbatim}
: (? (db nr +Ord 1 @Ord) (map @L @Ord pos))
 @Ord={3-7} @L=({4-1} {4-2} {4-3})
 @Ord={3-7} @L=({4-2} {4-3})
 @Ord={3-7} @L=({4-3})
-> NIL
\end{wideverbatim}

 
\section*{\texttt{(mapc 'fun 'lst ..) -> any}}
\label{sec:func-ref-M-(mapc 'fun 'lst ..) -> any}


Applies \texttt{fun} to each element of \texttt{lst}. When additional \texttt{lst} arguments
are given, their elements are also passed to \texttt{fun}. Returns the result
of the last application. See also \texttt{map}, \texttt{maplist}, \texttt{mapcar}, \texttt{mapcon},
\texttt{mapcan} and \texttt{filter}.


\begin{wideverbatim}
: (mapc println (1 2 3 4) '(A B C))
1 A
2 B
3 C
4 NIL
-> NIL
\end{wideverbatim}

 
\section*{\texttt{(mapcan 'fun 'lst ..) -> lst}}
\label{sec:func-ref-M-(mapcan 'fun 'lst ..) -> lst}


Applies \texttt{fun} to each element of \texttt{lst}. When additional \texttt{lst} arguments
are given, their elements are also passed to \texttt{fun}. Returns a
(destructively) concatenated list of all results. See also \texttt{map},
\texttt{mapc}, \texttt{maplist}, \texttt{mapcar}, \texttt{mapcon}, \texttt{filter}.


\begin{wideverbatim}
: (mapcan reverse '((a b c) (d e f) (g h i)))
-> (c b a f e d i h g)
\end{wideverbatim}

 
\section*{\texttt{(mapcar 'fun 'lst ..) -> lst}}
\label{sec:func-ref-M-(mapcar 'fun 'lst ..) -> lst}


Applies \texttt{fun} to each element of \texttt{lst}. When additional \texttt{lst} arguments
are given, their elements are also passed to \texttt{fun}. Returns a list of
all results. See also \texttt{map}, \texttt{mapc}, \texttt{maplist}, \texttt{mapcon}, \texttt{mapcan} and
\texttt{filter}.


\begin{wideverbatim}
: (mapcar + (1 2 3) (4 5 6))
-> (5 7 9)
: (mapcar '((X Y) (+ X (* Y Y))) (1 2 3 4) (5 6 7 8))
-> (26 38 52 68)
\end{wideverbatim}

 
\section*{\texttt{(mapcon 'fun 'lst ..) -> lst}}
\label{sec:func-ref-M-(mapcon 'fun 'lst ..) -> lst}


Applies \texttt{fun} to \texttt{lst} and all successive CDRs. When additional \texttt{lst}
arguments are given, they are passed to \texttt{fun} in the same way. Returns a
(destructively) concatenated list of all results. See also \texttt{map},
\texttt{mapc}, \texttt{maplist}, \texttt{mapcar}, \texttt{mapcan} and \texttt{filter}.


\begin{wideverbatim}
: (mapcon copy '(1 2 3 4 5))
-> (1 2 3 4 5 2 3 4 5 3 4 5 4 5 5)
\end{wideverbatim}

 
\section*{\texttt{(maplist 'fun 'lst ..) -> lst}}
\label{sec:func-ref-M-(maplist 'fun 'lst ..) -> lst}


Applies \texttt{fun} to \texttt{lst} and all successive CDRs. When additional \texttt{lst}
arguments are given, they are passed to \texttt{fun} in the same way. Returns a
list of all results. See also \texttt{map}, \texttt{mapc}, \texttt{mapcar}, \texttt{mapcon},
\texttt{mapcan} and \texttt{filter}.


\begin{wideverbatim}
: (maplist cons (1 2 3) '(A B C))
-> (((1 2 3) A B C) ((2 3) B C) ((3) C))
\end{wideverbatim}

 
\section*{\texttt{(maps 'fun 'sym ['lst ..]) -> any}}
\label{sec:func-ref-M-(maps 'fun 'sym ['lst ..]) -> any}


Applies \texttt{fun} to all properties of \texttt{sym}. When additional \texttt{lst}
arguments are given, their elements are also passed to \texttt{fun}. Returns
the result of the last application. See also \texttt{putl} and \texttt{getl}.


\begin{wideverbatim}
: (put 'X 'a 1)
-> 1
: (put 'X 'b 2)
-> 2
: (put 'X 'flg T)
-> T
: (getl 'X)
-> (flg (2 . b) (1 . a))
: (maps println 'X '(A B))
flg A
(2 . b) B
(1 . a) NIL
-> NIL
\end{wideverbatim}

 
\section*{\texttt{(mark 'sym|0 ['NIL | 'T | '0]) -> flg}}
\label{sec:func-ref-M-(mark 'sym|0 ['NIL | 'T | '0]) -> flg}


Tests, sets or resets a mark for \texttt{sym} in the database (for a second
argument of \texttt{NIL}, \texttt{T} or \texttt{0}, respectively), and returns the old value.
The marks are local to the current process (not stored in the database),
and vanish when the process terminates. If the first argument is zero,
all marks are cleared.


\begin{wideverbatim}
: (pool "db")
-> T
: (mark '{1} T)      # Mark
-> NIL
: (mark '{1})        # Test
-> T                 # -> marked
: (mark '{1} 0)      # Unmark
-> T
: (mark '{1})        # Test
-> NIL               # -> unmarked
\end{wideverbatim}

 
\section*{\texttt{(match 'lst1 'lst2) -> flg}}
\label{sec:func-ref-M-(match 'lst1 'lst2) -> flg}


Takes \texttt{lst1} as a pattern to be matched against \texttt{lst2}, and returns \texttt{T}
when successful. Atoms must be equal, and sublists must match
recursively. Symbols in the pattern list with names starting with an
at-mark ``\texttt{@}'' (see \texttt{pat?}) are taken as wildcards. They can match zero,
one or more elements, and are bound to the corresponding data. See also
\texttt{chop}, \texttt{split} and \texttt{fill}.


\begin{wideverbatim}
: (match '(@A is @B) '(This is a test))
-> T
: @A
-> (This)
: @B
-> (a test)
: (match '(@X (d @Y) @Z) '((a b c) (d (e f) g) h i))
-> T
: @X
-> ((a b c))
: @Y
-> ((e f) g)
: @Z
-> (h i)
\end{wideverbatim}

 
\section*{\texttt{(max 'any ..) -> any}}
\label{sec:func-ref-M-(max 'any ..) -> any}


Returns the largest of all \texttt{any} arguments. See also
\emph{min} and \emph{Comparing}.


\begin{wideverbatim}
: (max 2 'a 'z 9)
-> z
: (max (5) (2 3) 'X)
-> (5)
\end{wideverbatim}

 
\section*{\texttt{(maxKey 'tree ['any1 ['any2]]) -> any}}
\label{sec:func-ref-M-(maxKey 'tree ['any1 ['any2]]) -> any}


Returns the largest key in a database tree. If a minimal key \texttt{any1}
and/or a maximal key \texttt{any2} is given, the largest key from that range is
returned. See also \texttt{tree}, \texttt{leaf}, \texttt{minKey} and \texttt{genKey}.


\begin{wideverbatim}
: (maxKey (tree 'nr '+Item))
-> 7
: (maxKey (tree 'nr '+Item) 3 5)
-> 5
\end{wideverbatim}

 
\section*{\texttt{(maxi 'fun 'lst ..) -> any}}
\label{sec:func-ref-M-(maxi 'fun 'lst ..) -> any}


Applies \texttt{fun} to each element of \texttt{lst}. When additional \texttt{lst} arguments
are given, their elements are also passed to \texttt{fun}. Returns that element
from \texttt{lst} for which \texttt{fun} returned a maximal value. See also \texttt{mini} and
\texttt{sort}.


\begin{wideverbatim}
: (setq A 1  B 2  C 3)
-> 3
: (maxi val '(A B C))
-> C
: (maxi                          # Symbol with largest list value
   '((X)
      (and (pair (val X)) (size @)) )
   (what) )
-> *History
\end{wideverbatim}

 
\section*{\texttt{(member 'any 'lst) -> any}}
\label{sec:func-ref-M-(member 'any 'lst) -> any}


Returns the tail of \texttt{lst} that starts with \texttt{any} when \texttt{any} is a member
of \texttt{lst}, otherwise \texttt{NIL}. See also \texttt{memq}, \texttt{assoc} and \texttt{idx}.


\begin{wideverbatim}
: (member 3 (1 2 3 4 5 6))
-> (3 4 5 6)
: (member 9 (1 2 3 4 5 6))
-> NIL
: (member '(d e f) '((a b c) (d e f) (g h i)))
-> ((d e f) (g h i))
\end{wideverbatim}

 
\section*{\texttt{member/2}}
\label{sec:func-ref-M-member/2}


\emph{Pilog} predicate that succeeds if the the first
argument is a member of the list in the second argument. See also
\texttt{equal/2} and \texttt{member}.


\begin{wideverbatim}
:  (? (member @X (a b c)))
 @X=a
 @X=b
 @X=c
-> NIL
\end{wideverbatim}

 
\section*{\texttt{(memq 'any 'lst) -> any}}
\label{sec:func-ref-M-(memq 'any 'lst) -> any}


Returns the tail of \texttt{lst} that starts with \texttt{any} when
\texttt{any} is a member of \texttt{lst}, otherwise \texttt{NIL}.
\texttt{==} is used for comparison (pointer equality). See also
\texttt{member}, \texttt{mmeq}, \texttt{asoq}, \texttt{delq} and
\emph{Comparing}.


\begin{wideverbatim}
: (memq 'c '(a b c d e f))
-> (c d e f)
: (memq (2) '((1) (2) (3)))
-> NIL
\end{wideverbatim}

 
\section*{\texttt{(meta 'obj|typ 'sym ['sym2|cnt ..]) -> any}}
\label{sec:func-ref-M-(meta 'obj|typ 'sym ['sym2|cnt ..]) -> any}


Fetches a property value \texttt{any}, by searching the property lists of the
classes and superclasses of \texttt{obj}, or the classes in \texttt{typ}, for the
property key \texttt{sym}, and by applying the \texttt{get} algorithm to the following
optional arguments. See also \texttt{var:}.


\begin{wideverbatim}
: (setq A '(B))            # Be 'A' an object of class 'B'
-> (B)
: (put 'B 'a 123)
-> 123
: (meta 'A 'a)             # Fetch 'a' from 'B'
-> 123
\end{wideverbatim}

 
\section*{\texttt{(meth 'obj ['any ..]) -> any}}
\label{sec:func-ref-M-(meth 'obj ['any ..]) -> any}


This function is usually not called directly, but is used by \texttt{dm} as a
template to initialize the \texttt{VAL} of message symbols. It searches for
itself in the methods of \texttt{obj} and its classes and superclasses, and
executes that method. An error \texttt{''Bad message''} is issued if the search
is unsuccessful. See also \texttt{OO Concepts}, \texttt{method}, \texttt{send} and \texttt{try}.


\begin{wideverbatim}
: meth
-> 67283504    # Value of 'meth'
: stop>
-> 67283504    # Value of any message
\end{wideverbatim}

 
\section*{\texttt{(method 'msg 'obj) -> fun}}
\label{sec:func-ref-M-(method 'msg 'obj) -> fun}


Returns the function body of the method that would be executed upon
sending the message \texttt{msg} to the object \texttt{obj}. If the message cannot be
located in \texttt{obj}, its classes and superclasses, \texttt{NIL} is returned. See
also \texttt{OO Concepts}, \texttt{send}, \texttt{try}, \texttt{meth}, \texttt{super}, \texttt{extra}, \texttt{class}.


\begin{wideverbatim}
: (method 'mis> '+Number)
-> ((Val Obj) (and Val (not (num? Val)) "Numeric input expected"))
\end{wideverbatim}

 
\section*{\texttt{(min 'any ..) -> any}}
\label{sec:func-ref-M-(min 'any ..) -> any}


Returns the smallest of all \texttt{any} arguments. See also
\emph{max} and \emph{Comparing}.


\begin{wideverbatim}
: (min 2 'a 'z 9)
-> 2
: (min (5) (2 3) 'X)
-> X
\end{wideverbatim}

 
\section*{\texttt{(minKey 'tree ['any1 ['any2]]) -> any}}
\label{sec:func-ref-M-(minKey 'tree ['any1 ['any2]]) -> any}


Returns the smallest key in a database tree. If a minimal key \texttt{any1}
and/or a maximal key \texttt{any2} is given, the smallest key from that range
is returned. See also \texttt{tree}, \texttt{leaf}, \texttt{maxKey} and \texttt{genKey}.


\begin{wideverbatim}
: (minKey (tree 'nr '+Item))
-> 1
: (minKey (tree 'nr '+Item) 3 5)
-> 3
\end{wideverbatim}

 
\section*{\texttt{(mini 'fun 'lst ..) -> any}}
\label{sec:func-ref-M-(mini 'fun 'lst ..) -> any}


Applies \texttt{fun} to each element of \texttt{lst}. When additional \texttt{lst} arguments
are given, their elements are also passed to \texttt{fun}. Returns that element
from \texttt{lst} for which \texttt{fun} returned a minimal value. See also \texttt{maxi} and
\texttt{sort}.


\begin{wideverbatim}
: (setq A 1  B 2  C 3)
-> 3
: (mini val '(A B C))
-> A
\end{wideverbatim}

 
\section*{\texttt{(mix 'lst cnt|'any ..) -> lst}}
\label{sec:func-ref-M-(mix 'lst cnt|'any ..) -> lst}


Builds a list from the elements of the argument \texttt{lst}, as specified by
the following \texttt{cnt|'any} arguments. If such an argument is a number, the
\texttt{cnt}'th element from \texttt{lst} is taken, otherwise that argument is
evaluated and the result is used.


\begin{wideverbatim}
: (mix '(a b c d) 3 4 1 2)
-> (c d a b)
: (mix '(a b c d) 1 'A 4 'D)
-> (a A d D)
\end{wideverbatim}

 
\section*{\texttt{(mmeq 'lst 'lst) -> any}}
\label{sec:func-ref-M-(mmeq 'lst 'lst) -> any}


Returns the tail of the second argument \texttt{lst} that starts with a member
of the first argument \texttt{lst}, otherwise \texttt{NIL}. \texttt{==} is used for
comparison (pointer equality). See also \texttt{member}, \texttt{memq}, \texttt{asoq} and
\texttt{delq}.


\begin{wideverbatim}
: (mmeq '(a b c) '(d e f))
-> NIL
: (mmeq '(a b c) '(d b x))
-> (b x)
\end{wideverbatim}

 
\section*{\texttt{(money 'num ['sym]) -> sym}}
\label{sec:func-ref-M-(money 'num ['sym]) -> sym}


Formats a number \texttt{num} into a digit string with two decimal places,
according to the current \texttt{locale}. If an additional currency name is
given, it is appended (separated by a space). See also \texttt{telStr},
\texttt{datStr} and \texttt{format}.


\begin{wideverbatim}
: (money 123456789)
-> "1,234,567.89"
: (money 12345 "EUR")
-> "123.45 EUR"
: (locale "DE" "de")
-> NIL
: (money 123456789 "EUR")
-> "1.234.567,89 EUR"
\end{wideverbatim}

 
\section*{\texttt{(more 'lst ['fun]) -> flg}}
\label{sec:func-ref-M-(more 'lst ['fun]) -> flg}


\texttt{(more 'cls) -> any}

Displays the elements of \texttt{lst} (first form), or the type and methods of
\texttt{cls} (second form). \texttt{fun} defaults to \texttt{print}. In the second form, the
method definitions of \texttt{cls} are pretty-printed with \texttt{pp}. After each
step, \texttt{more} waits for console input, and terminates when a non-empty
line is entered. In that case, \texttt{T} is returned, otherwise (when end of
data is reached) \texttt{NIL}. See also \texttt{query} and \texttt{show}.


\begin{wideverbatim}
: (more (all))                         # Display all internal symbols
inc>
leaf
nil
inc!
accept.                                # Stop
-> T

: (more (all) show)                    # 'show' all internal symbols
inc> 67292896
   *Dbg ((859 . "lib/db.l"))

leaf ((Tree) (let (Node (cdr (root Tree)) X)
(while (val Node) (setq X (cadr @) Node (car @))) (cddr X)))
*Dbg ((173 . "lib/btree.l"))

nil 67284680
   T (((@X) (@ not (-> @X))))
.                                      # Stop
-> T

: (more '+Link)                        # Display a class
(+relation)

(dm mis> (Val Obj)
   (and
      Val
      (nor (isa (: type) Val) (canQuery Val))
      "Type error" ) )

(dm T (Var Lst)
   (unless (=: type (car Lst)) (quit "No Link" Var))
   (super Var (cdr Lst)) )

-> NIL
\end{wideverbatim}

 
\section*{\texttt{(msg 'any ['any ..]) -> any}}
\label{sec:func-ref-M-(msg 'any ['any ..]) -> any}


Prints \texttt{any} with \texttt{print}, followed by all \texttt{any} arguments (printed with
\texttt{prin}) and a newline, to standard error. The first \texttt{any} argument is
returned.


\begin{wideverbatim}
: (msg (1 a 2 b 3 c) " is a mixed " "list")
(1 a 2 b 3 c) is a mixed list
-> (1 a 2 b 3 c)
\end{wideverbatim}


